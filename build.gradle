buildscript {
	repositories {
		jcenter()
		maven {
			name "forge"
			url "http://files.minecraftforge.net/maven"
		}
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.8"
		classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7"
	}
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "com.jfrog.bintray"

sourceCompatibility = 1.8
targetCompatibility = 1.8

def getVersion() {
	if (System.getenv("TRAVIS_TAG"))
		return System.getenv("TRAVIS_TAG")
	return "0.0.0-x"
}
task deobfJar(type: Jar, dependsOn: "reobfJar") {
	from sourceSets.main.output
	from sourceSets.api.output
	classifier = "dev"
}
task sourcesJar(type: Jar, dependsOn: "sourceJar") {
	from sourceSets.main.allJava
	from sourceSets.api.allJava
	classifier = "sources"
}

version = getVersion()
group = "szewek.mcflux"
archivesBaseName = "mcflux"

minecraft {
	version = "1.10.2-12.18.1.2076"
	runDir = "run"
	mappings = "snapshot_20160828"
	replace "@VERSION@", project.version
	replaceIn "R.java"
}

bintray {
	user = System.getenv("BINTRAY_USER")
	key = System.getenv("BINTRAY_KEY")
	configurations = ["archives"]
	publish = true
	override = true
	pkg {
		repo = "MC"
		name = project.group
		version {
			name = project.version
		}
	}
}

repositories {
	maven {url "http://dvs1.progwml6.com/files/maven"}
	maven {url "http://mobiusstrip.eu/maven"}
	maven {url "http://maven.ic2.player.to"}
	maven {url "http://maven.epoxide.xyz"}
}

dependencies {
	deobfCompile "mezz.jei:jei_1.10.2:3.9.0.246"
	deobfCompile "mcp.mobius.waila:Waila:1.7.0-B3_1.9.4"
	deobfCompile "net.industrial-craft:industrialcraft-2:2.6.54-ex110:api"
	deobfCompile "net.darkhax.tesla:Tesla:1.10.2-1.2.1.49"
}

jar {
	//from sourceSets.api.output
	//exclude "blusunrize/**"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}
curseforge {
	apiKey = ""
	if (System.env.CURSEFORGE_KEY)
		apiKey = System.env.CURSEFORGE_KEY
	project {
		id = "248942"
		releaseType = "release"
		changelogType = "markdown"
		changelog = file("CHANGELOG.md")
	}
}
artifacts {
	archives deobfJar
	archives sourcesJar
}