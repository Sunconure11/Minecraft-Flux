buildscript {
	repositories {
		jcenter()
		maven {
			name "forge"
			url "http://files.minecraftforge.net/maven"
		}
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.8"
	}
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"

sourceCompatibility = 1.8
targetCompatibility = 1.8

def getVersion() {
	if (System.env.TRAVIS_TAG)
		return System.env.TRAVIS_TAG
	if (System.env.BUILD_VERSION)
		return System.env.BUILD_VERSION
	return "0.0.0-x"
}
task deobfJar(type: Jar, dependsOn: "reobfJar") {
	from sourceSets.main.output
	from sourceSets.api.output
	classifier = "dev"
}

version = getVersion()
group = "szewek.mcflux"
archivesBaseName = "mcflux"

minecraft {
	version = "1.10.2-12.18.1.2065"
	runDir = "run"
	mappings = "snapshot_20160821"
	replace "@VERSION@", project.version
	replaceIn "R.java"
}

repositories {
	maven {url "http://dvs1.progwml6.com/files/maven"}
	maven {url "http://mobiusstrip.eu/maven"}
}

dependencies {
	deobfCompile "mezz.jei:jei_1.10.2:3.7.8.234"
	deobfCompile "mcp.mobius.waila:Waila:1.7.0-B3_1.9.4"
}

// QUICK FIX
jar {
	from sourceSets.main.output
	from sourceSets.api.output
}
sourceJar {
	from sourceSets.api.allSource
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}
curseforge {
	apiKey = "0"
	if (System.env.CURSEFORGE_KEY)
		apiKey = System.env.CURSEFORGE_KEY
	project {
		id = "248942"
		releaseType = "beta"
		changelogType = "markdown"
		changelog = file("CHANGELOG.md")
		addArtifact sourceJar
		addArtifact deobfJar
	}
}

artifacts {
	archives deobfJar
}